set dotenv-load := false

_default:
    @just --list

HOST := "erebe@192.168.1.179"

wireguard:
  sops exec-env ../../secrets/wireguard.yml 'cp wireguard/wg0.conf secrets_decrypted/; for i in $(env | grep _KEY | cut -d = -f1); do sed -i "s#__${i}__#${!i}#g" secrets_decrypted/wg0.conf ; done'
  ssh {{HOST}} "cat /etc/wireguard/wg0.conf" | diff  - secrets_decrypted/wg0.conf || exit 0
  rsync --rsync-path="sudo rsync" secrets_decrypted/wg0.conf {{HOST}}:/etc/wireguard/wg0.conf
  rsync --rsync-path="sudo rsync" config/allow-router-advertise {{HOST}}:/etc/network/if-pre-up.d/allow-router-advertise
  ssh {{HOST}} "sudo systemctl restart wg-quick@wg0.service && sudo systemctl enable wg-quick@wg0.service"

package:
  ssh {{HOST}} "sudo apt-get update && sudo apt-get install -y rsync nfs-common"
  ssh {{HOST}} "curl -fsSL https://raw.githubusercontent.com/helm/helm/main/scripts/get-helm-3 | bash"
  ssh {{HOST}} "curl -L https://github.com/cilium/cilium-cli/releases/download/v0.15.5/cilium-linux-amd64.tar.gz | tar xzvf - cilium"
  ssh {{HOST}} "curl -L https://github.com/derailed/k9s/releases/download/v0.27.4/k9s_Linux_amd64.tar.gz | tar xzvf - k9s"

k3s:
  ssh {{HOST}} "sudo mkdir -p /etc/rancher/k3s"
  rsync --rsync-path="sudo rsync" k3s/config.yaml {{HOST}}:/etc/rancher/k3s/config.yaml
  ssh {{HOST}} "curl -sfL https://get.k3s.io | INSTALL_K3S_VERSION="v1.30.8+k3s1" sh -s - server"
  rsync k3s/cilium.yaml {{HOST}}:cilium.yaml
  rsync k3s/cilium-install.sh {{HOST}}:cilium-install.sh
  ssh {{HOST}} "./cilium-install.sh"


